| Severity | Title |
| -------- | -------- | 
|H-01 |Curves can be changed arbitrarily.|
|M-01 |Flawed design of Symbol Duplication Check Could Lead to Unexpected DOS|
|M-02 |First buyer can not buyCurvesToken with amount greater than 1 due to underflow in getPrice|
|L-01 |curvesTokenSubject could buy with amount = 0 and stuck the following purchases |


## [H-01]  Curves can be changed arbitrarily.

## Vulnerability details
### Impact
There is a lack of access control in setCurves.
curves can be set arbitrarily.




### Proof of Concept
https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L35



## Recommended Mitigation Steps
Add appropriateaccess control to setCurves.

## [M-01]  Flawed design of Symbol Duplication Check Could Lead to Unexpected DOS

## Vulnerability details
### Impact
The contract Curves uses SymbolToSubject to prevent duplication of symbol for ERC20-representation token. Any transaction that attempts to create a token with an already-existed symbol will revert due to the check if (symbolToSubject[symbol] != address(0)) revert InvalidERC20Metadata();. However, the design is flawed so the following unexpected DOS of _deployERC20 would occur and any function associated (Ex. mint, withdraw) would get affected. As a consequence, the key feature Token Export to ERC20 will not work normally.

1. The _deployERC20 deals with the default symbol value CURVES by appending a _curvesTokenCounter at the end, this would make the default deploy proceed without failure. However, if a malicious user deploys a token called CURVES1(or CURVES2, ...), the _curvesTokenCounter can not work anymore. Any deploy transaction whose symbol is not manually set by users(thus symbol value is CURVES) will always revert.
2. The _deployERC20 associated function could be monitored and front-run. A malicious user can call the mint or buyCurvesTokenWithName function with exactly the same symbol, thus the victim will receive an unexpected revert.
### Proof of Concept
To prevent ERC20 token symbol duplication, the contract Curves implement the following design :

1. In function mint, before calling _mint and _deployERC20, the function checks if the name or symbol is empty. If so, the default value is used.
```
        if (
            keccak256(abi.encodePacked(externalCurvesTokens[curvesTokenSubject].name)) ==
            keccak256(abi.encodePacked("")) ||
            keccak256(abi.encodePacked(externalCurvesTokens[curvesTokenSubject].symbol)) ==
            keccak256(abi.encodePacked(""))
        ) {
            externalCurvesTokens[curvesTokenSubject].name = DEFAULT_NAME;
            externalCurvesTokens[curvesTokenSubject].symbol = DEFAULT_SYMBOL;
        }
```
2. In the function _deployERC20 triggered by _mint, the counter variable _curvesTokenCounter is used to deal with default input. So for default inputs, the symbol should be like CURVES1, CURVES2, CURVES3 ...
```
        if (keccak256(bytes(symbol)) == keccak256(bytes(DEFAULT_SYMBOL))) {
            _curvesTokenCounter += 1;
            name = string(abi.encodePacked(name, " ", Strings.toString(_curvesTokenCounter)));
            symbol = string(abi.encodePacked(symbol, Strings.toString(_curvesTokenCounter)));
        }

        if (symbolToSubject[symbol] != address(0)) revert InvalidERC20Metadata();
```
3. The check if (symbolToSubject[symbol] != address(0)) revert InvalidERC20Metadata(); will revert if the symbol has already been used.
But the design is flawed and may lead to unexpected situation.


### 1. _curvesTokenCounter could fail and block all default deploy transaction.
For default input, symbol = string(abi.encodePacked(symbol, Strings.toString(_curvesTokenCounter))) will generate a new symbol associated with the current _curvesTokenCounter. However, If a malicious user mints a token like symbol = CURVES2, the initial design would be broken. Thus if an user doesn't set symbol manually, he can not deploy the token anymore.

Consider the following scenario:

- Zed doesn't set symbol, so the default value is used, and he can mint the token successfully, thus CURVES1 is used, and the _curvesTokenCounter is updated to 1.
- A malicious user Bob could manually set the symbol to CURVES2, since this is not the default value, _curvesTokenCounter remains 1.
- Another user jack doesn't set symbol, and he also wants to mint. But this time, since CURVES2 is already used, the function will revert due to the duplication check of CURVES2 and string(abi.encodePacked("CURVES", Strings.toString(2)));. So, all following _deployERC20 calls without manually setting the symbol would revert.
We have a POC here.
```
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console2} from "forge-std/Test.sol";
import {Curves} from "../src/Curves.sol";
import {CurvesERC20Factory} from "../src/CurvesERC20Factory.sol";
import "forge-std/console.sol";

contract CurveTest is Test {
    error InvalidERC20Metadata();

    Curves public curves;
    address internal constant alice = address(1);
    address internal constant zed = address(4);
    address internal constant bob = address(2);
    address internal constant jack = address(3);

    function setUp() public {
        vm.deal(alice, 1 ether);
        vm.deal(bob, 1 ether);
        vm.deal(jack, 1 ether);
        vm.deal(zed, 1 ether);
        vm.startPrank(alice);
        CurvesERC20Factory factory = new CurvesERC20Factory();
        curves = new Curves(address(factory),alice);
        console.log(curves.curvesERC20Factory());
    }

    function test_revertByInject() public {
        vm.startPrank(zed);
        curves.mint(zed);
        vm.startPrank(bob);
        console.log("BOB set Symbol to CURVES1 and mint");
        curves.setNameAndSymbol(bob,"Test","CURVES2");
        curves.mint(bob);
        assertEq(bob,curves.symbolToSubject("CURVES2"));
        vm.startPrank(jack);
        vm.expectRevert(InvalidERC20Metadata.selector);
        curves.mint(jack);
    }
}
```
In the POC, Curves::mint(jack) will finally revert due to InvalidERC20Metadata(). Thus, by setting the symbol as CURVES2, the initial design to handle default-input cases would fail forever. As a consequence, the contract is unable to export Token to ERC20.
### 2. The mechanism is vulnerable to front-run attack.
Even if a user could call setNameAndSymbol to set the name and symbol of the token, his call on mint or other functions could still be front-run and cause Revert.

Consider the following situation:

- Alice calls setNameAndSymbol to set the symbol to alice.
- Before she calls mint, bob is monitoring the mem-pool and quickly sends a transaction to set the symbol to alice and mint.
- Alice's transaction would revert unexpectedly.
We have a POC here.
```
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console2} from "forge-std/Test.sol";
import {Curves} from "../src/Curves.sol";
import {CurvesERC20Factory} from "../src/CurvesERC20Factory.sol";
import "forge-std/console.sol";

contract CurveTest is Test {
    error InvalidERC20Metadata();

    Curves public curves;
    address internal constant alice = address(1);
    address internal constant zed = address(4);
    address internal constant bob = address(2);
    address internal constant jack = address(3);

    function setUp() public {
        vm.deal(alice, 1 ether);
        vm.deal(bob, 1 ether);
        vm.deal(jack, 1 ether);
        vm.deal(zed, 1 ether);
        vm.startPrank(alice);
        CurvesERC20Factory factory = new CurvesERC20Factory();
        curves = new Curves(address(factory),alice);
        console.log(curves.curvesERC20Factory());
    }

    function test_revertByFrontrun() public {
        vm.startPrank(alice);
        curves.setNameAndSymbol(alice,"Test","alice");
        vm.startPrank(bob);
        curves.setNameAndSymbol(bob,"Test","alice");
        curves.mint(bob);
        vm.startPrank(alice);
        vm.expectRevert(InvalidERC20Metadata.selector);
        curves.mint(alice);
    }    
}
```
alice's transaction would revert due to InvalidERC20Metadata if she is front-run by bob.
## Recommended Mitigation Steps
For the first scenario, the contract Curves can update _curvesTokenCounter in a while loop inside _deployERC20.
For example, the code could be like:
```
        if (keccak256(bytes(symbol)) == keccak256(bytes(DEFAULT_SYMBOL))) { // @follow-up Counter May get Blocked via name. @follow-up may get frontrunned?
            // _curvesTokenCounter += 1;
            // name = string(abi.encodePacked(name, " ", Strings.toString(_curvesTokenCounter)));
            // symbol = string(abi.encodePacked(symbol, Strings.toString(_curvesTokenCounter)));
            while (true) {
                _curvesTokenCounter += 1;
                string memory name2 = string(abi.encodePacked(name, " ", Strings.toString(_curvesTokenCounter)));
                string memory symbol2 = string(abi.encodePacked(symbol, Strings.toString(_curvesTokenCounter)));
                if (symbolToSubject[symbol2] == address(0)) {
                    name = name2;
                    symbol = symbol2;
                    break;
                }
            }    
        } 
```
Or just check the first few bytes of input symbol to see if the symbol starts with CURVES.
For the second scenario, there's really not much can be done with front-running. Maybe some changes could be made in the design side. For example, the symbol could be registered before deploy.

## [M-02]  First buyer can not buyCurvesToken with amount greater than 1 due to underflow in getPrice 

## Vulnerability details
### Impact
In the function getPrice, in the calculation of sum2, the formula ((supply - 1 + amount) * (supply + amount) * (2 * (supply - 1 + amount) + 1)) / 6 is used when the condition supply == 0 && amount == 1 is not fulfilled. This is meant to work for all cases. However, if supply == 0 && amount > 0, the supply -1 + amount will throw an error due to the underflow of supply - 1. This means that, for the first purchase, it will always fail when amount > 1. This will greatly influence the user's experience as this condition is never explained nor considered.
### Proof of Concept
The related code is shown below:
```
    function getPrice(uint256 supply, uint256 amount) public pure returns (uint256) {
        uint256 sum1 = supply == 0 ? 0 : ((supply - 1) * (supply) * (2 * (supply - 1) + 1)) / 6;
        uint256 sum2 = supply == 0 && amount == 1
            ? 0
            : ((supply - 1 + amount) * (supply + amount) * (2 * (supply - 1 + amount) + 1)) / 6;
        uint256 summation = sum2 - sum1;
        return (summation * 1 ether) / 16000;
    }
```
Clearly, supply-1+amount is meant to work for any supply =0 && amount != 1, but the deduction occurs before addition, thus supply - 1 will revert.

The POC is shown below:

```
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console2} from "forge-std/Test.sol";
import {Curves} from "../src/Curves.sol";
import {CurvesERC20Factory} from "../src/CurvesERC20Factory.sol";
import "forge-std/console.sol";

contract CurveTest is Test {
    error InvalidERC20Metadata();
    error UnauthorizedCurvesTokenSubject();

    Curves public curves;
    address internal constant alice = address(1);
    address internal constant zed = address(4);
    address internal constant bob = address(2);
    address internal constant jack = address(3);

    function setUp() public {
        vm.deal(alice, 100 ether);
        vm.deal(bob, 100 ether);
        vm.deal(jack, 100 ether);
        vm.deal(zed, 100 ether);
        vm.startPrank(alice);
        CurvesERC20Factory factory = new CurvesERC20Factory();
        curves = new Curves(address(factory),alice);
        console.log(curves.curvesERC20Factory());
    }

    function test_revertWhenAmoutGT1() public {
        vm.startPrank(alice);
        uint fee = curves.getBuyPriceAfterFee(alice,1);
        console.log("Work as expected, when supply = 0 && amount = 1, fee = ", fee);
        vm.expectRevert();
        console.log("Will revert Due to Panic : panic: arithmetic underflow or overflow (0x11) when supply = 0 && amount = 2");
        uint fee2 = curves.getBuyPriceAfterFee(alice,2);
    }
}
```
The log shows:
```
  Work as expected, when supply = 0 && amount = 1, fee =  0
  Will revert Due to Panic : panic: arithmetic underflow or overflow (0x11) when supply = 0 && amount = 2
```
And the call reverts due to panic: arithmetic underflow or overflow (0x11). This is an unexpected error that is never considered nor handled. This means that, for the first purchase, it will always fail when amount > 1.
## Recommended Mitigation Steps
Just change the equation supply - 1 + amount to supply + amount -1 so that it works for all cases.


## [L-01]  curvesTokenSubject could buy with amount = 0 and stuck the following purchases

## Vulnerability details
### Impact
CurvesTokenSubject can call buyCurvesTokenWithName, buyCurvesTokenForPresale and buyCurvesToken to buy tokens. But if he calls buyCurvesTokenWithName or buyCurvesTokenForPresale to set token info or presale info but with amount = 0, the transaction would proceed as there is no check that amount != 0. Thus the token info has been set or presale has started, but other users still could not buy tokens due to the check  if (!(supply > 0 || curvesTokenSubject == msg.sender)) revert UnauthorizedCurvesTokenSubject();. Thus all purchases are stuck.
### Proof of Concept
In the function buyCurvesTokenWithName, buyCurvesTokenForPresale and buyCurvesToken or _buyCurvesToken, there is no check that the amount is non-zero. Consider the following situation:

1. Alice calls buyCurvesTokenForPresale to set presale, but his input for amount is 0.
2. The presale has started. Whitelisted user Bob wants to buy via buyCurvesTokenWhitelisted, but the function will revert due to if (!(supply > 0 || curvesTokenSubject == msg.sender)) revert UnauthorizedCurvesTokenSubject();. He has to wait for Alice to buy again manually. If not, his whitelist will be wasted.

## Recommended Mitigation Steps
Add proper checks that amount > 0.